# o objetivo é que haja 2 jarros, sendo um chamado de j1 e outro de j2. j1 inicia com 100L e j2 com 20L. o objetivo final é que o j1 tenha 60L.

sp {water-jug*propose*initialize-water-jug
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize-water-jug)}

sp {water-jug*apply*initialize-water-jug
   (state <s> ^operator.name initialize-water-jug)
-->
   (<s> ^name water-jug
        ^jug <i> <j>)
   (<i> ^volume 100
        ^contents 100
        ^name j1)
   (<j> ^volume 20
        ^contents 20
        ^name j2)}

sp {water-jug*elaborate*empty
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^contents <c>
        ^volume <v>)
-->
   (<j> ^empty (- <v> <c>))}

sp {water-jug*propose*fill
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^empty > 0)
-->
   (<s> ^operator <o> + =)
   (<o> ^name fill
        ^fill-jug <j>)}

sp {water-jug*apply*fill
   (state <s> ^name water-jug
              ^operator <o>
              ^jug <j>)
   (<o> ^name fill
        ^fill-jug <j>)
   (<j> ^volume <volume>
        ^contents <contents>)
-->
   (<j> ^contents <volume>
        ^contents <contents> -)}

sp {water-jug*propose*empty
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^contents > 0)
-->
   (<s> ^operator <o> + =)
   (<o> ^name empty
        ^empty-jug <j>)}

sp {water-jug*apply*empty
   (state <s> ^name water-jug
              ^operator <o>
              ^jug <j>)
   (<o> ^name empty
        ^empty-jug <j>)
   (<j> ^contents <contents>)
-->
   (<j> ^contents 0
        ^contents <contents> -)}

sp {water-jug*propose*pour
   (state <s> ^name water-jug
              ^jug <i> { <> <i> <j> })
   (<i> ^contents > 0)
   (<j> ^empty > 0)
-->
   (<s> ^operator <o> + =)
   (<o> ^name pour
        ^empty-jug <i>
        ^fill-jug <j>)}

sp {water-jug*apply*pour*not-empty-source
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name pour
        ^empty-jug <i>
        ^fill-jug <j>)
   (<j> ^contents <jcon>
        ^empty <jempty>)
   (<i> ^contents { <icon> <= <jempty> })
-->
   (<i> ^contents 0 <icon> -)
   (<j> ^contents (+ <jcon> <icon>) <jcon> -)}

sp {water-jug*apply*pour*empty-source
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name pour
        ^empty-jug <i>
        ^fill-jug <j>)
   (<i> ^contents { <icon> > <jempty> })
   (<j> ^volume <jvol>
        ^contents <jcon>
        ^empty <jempty>)
-->
   (<i> ^contents (- <icon> <jempty>) <icon> -)
   (<j> ^contents <jvol> <jcon> -)}

sp {water-jug*select*avoid*fill-after-empty
   (state <s> ^name water-jug
              ^operator <o> +
              ^last-operator <lo>)
   (<o> ^name fill
        ^fill-jug <j>)
   (<lo> ^name empty
         ^jug <j>)
-->
   (<s> ^operator <o> <)}

sp {water-jug*select*avoid*empty-after-fill
   (state <s> ^name water-jug
              ^operator <o> +
              ^last-operator <lo>)
   (<o> ^name empty
        ^empty-jug <j>)
   (<lo> ^name fill
         ^jug <j>)
-->
   (<s> ^operator <o> <)}

sp {water-jug*select*avoid*reverse-pour
   (state <s> ^name water-jug
              ^operator <o> +
              ^last-operator <lo>)
   (<o> ^name pour
        ^empty-jug <i>
        ^fill-jug <j>)
   (<lo> ^name pour
         ^empty-jug <j>
         ^fill-jug <i>)
-->
   (<s> ^operator <o> <)}

sp {water-jug*apply*record*last-operator*fill
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name fill
        ^fill-jug <j>)
-->
   (<s> ^last-operator <lo>)
   (<lo> ^name fill
         ^jug <j>)}

sp {water-jug*apply*record*last-operator*empty
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name empty
        ^empty-jug <j>)
-->
   (<s> ^last-operator <lo>)
   (<lo> ^name empty
         ^jug <j>)}

sp {water-jug*apply*record*last-operator*pour
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name pour
        ^empty-jug <i>
        ^fill-jug <j>)
-->
   (<s> ^last-operator <lo>)
   (<lo> ^name pour
         ^empty-jug <i>
         ^fill-jug <j>)}

sp {water-jug*apply*remove*old-last-operator
   (state <s> ^name water-jug
              ^operator <o>
              ^last-operator <old-lo>)
-->
   (<s> ^last-operator <old-lo> -)}

sp {water-jug*detect*goal*achieved
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^name j1
        ^contents 60)
-->
   (write (crlf) (crlf) |========================================|)
   (write (crlf) |  PROBLEMA RESOLVIDO!|)   
   (write (crlf) |========================================|)
   (halt)}

sp {water-jug*monitor*state 
   (state <s> ^name water-jug
              ^jug <j> <i>)
   (<j> ^name j1 ^contents <jcon>)
   (<i> ^name j2 ^contents <icon>)
--> 
   (write (crlf) |Estado: j1=| <jcon> |L j2=| <icon> |L|)}

sp {water-jug*monitor*operator-application*empty
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name empty
        ^empty-jug.name <name>
        ^empty-jug.volume <volume>)
-->
   (write (crlf) |  -> ESVAZIAR | <name> | de | <volume> |L|)}

sp {water-jug*monitor*operator-application*fill
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name fill 
        ^fill-jug.name <name>
        ^fill-jug.volume <volume>)
-->
   (write (crlf) |  -> ENCHER | <name> | de | <volume> |L|)}

sp {water-jug*monitor*operator-application*pour
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name pour 
        ^empty-jug <i>
        ^fill-jug <j>)
   (<i> ^name <iname> ^contents <icon>)
   (<j> ^name <jname> ^contents <jcon>)
-->
   (write (crlf) |  -> DESPEJAR de | <iname> | (| <icon> |L) para | <jname> | (| <jcon> |L)|)}
